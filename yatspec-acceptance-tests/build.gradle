apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
    maven {
        url "http://repo.bodar.com"
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'com.googlecode.yatspec:yatspec:214'
}

// display test names during execution from command line
test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Test Structure
sourceSets {
    acceptanceTest {
        java {
            srcDir 'src/acceptanceTest/java'
            compileClasspath += main.output
            runtimeClasspath += main.output
        }

        resources {
            srcDir 'src/acceptanceTest/resources'
        }
    }
}

task acceptanceTest(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

configurations {
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
}

acceptanceTest {
    testLogging {
        events "passed", "skipped", "failed"
    }
}