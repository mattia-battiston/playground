// basic java config
apply plugin: 'java'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


// intellij config
apply plugin: 'idea'
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


// use the application plugin to run the main class from command line
// usage: ./gradlew run
apply plugin:'application'
mainClassName = "com.example.helloworld.Main"
run {
    args 'server', 'dist/hello-world.yml'
}


// package as a "fat jar" (using https://github.com/rholder/gradle-one-jar)
// usage - to package: ./gradlew oneJar
// usage - to run the app: java -jar build/libs/hello-world-dropwizard.jar server dist/hello-world.yml
apply plugin: 'gradle-one-jar'
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
}
task oneJar(type: OneJar, group: "Package & Run") {
    mainClass = mainClassName
    archiveName = 'hello-world-dropwizard.jar'
}
artifacts {
    oneJar
}


// repositories for dependencies
repositories {
    mavenCentral()
}


// dependencies
project.ext {
    dropwizardVersion = '0.7.0'
}
dependencies {
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion

    testCompile 'junit:junit:4.11'
}


// gradle wrapper (to use gradlew)
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
    distributionUrl = 'http://services.gradle.org/distributions/gradle-2.0-all.zip'
}